program: item*
item: main | struct | impl | function

main: "main" block-expression

struct: "struct" identifier "{" struct-body "}"
struct-body: struct-member* struct-constructor*
struct-member: type identifier ";"
struct-constructor: "$" parameters block-expression

impl: "impl" type "{" function* "}"
function: "fn" identifier parameters "->" type block-expression
parameters: "(" [type identifier ("," type identifier)*] ")"

statement: loop | if | block | for | while | exit | delete | declaration | assignment ";"
expression: condition
block: "{" statement* "}"
declaration: "let" type identifier "=" expression ";"
assignment: expression "=" expression | expression
for: "for" [":" identifier] "(" declaration expression ";" assignment ")" block
while: "while" [":" identifier] expression block
exit: ("return" | "break" [":" label] | "result" [":" label]) [expression] ";"
delete: "delete" expression ";"
loop: "loop" [":" identifier] block
if: "if" expression block elif* [else]
elif: "else" "if" expression block

else: "else" block
constructor: ["new"] "$" identifier arguments
new-array: "new" type ("[" expression "]")+

condition: boolean-factor ("or" boolean-factor)*
boolean-factor: bitwise-term ("and" bitwise-term)*
bitwise-term: bitwise-xor ("|" bitwise-xor)*
bitwise-xor: bitwise-factor ("^" bitwise-factor)*
bitwise-factor: equality ("&" equality)*
equality: comparison (("==" | "!=") comparison)*
comparison: bitwise-shift (("<" | "<=" | ">" | ">=") bitwise-shift)*
bitwise-shift: arithmetic-term (("<<" | ">>") arithmetic-term)*
arithmetic-term: arithmetic-factor (("-" | "+") arithmetic-factor)*
arithmetic-factor: cast (("*" | "/" | "%") cast)*
cast: unary ["as" type]

unary: ("!" | "-" | "*" | "&")* function-call
function-call: member-access [arguments]
member-access: primary (("->" | ".") identifier | "[" expression "]")*
arguments: "(" [expression ("," expression)*] ")"
primary: new-array | constructor | loop | if | block | for | while | "(" expression ")" | literal | identifier


type: unit-type ("[]" ["&"])*
unit-type: (primitive-type | identifier) "*"* ["&"]
primitive-type: "int" | "double" | "bool" | "char";
