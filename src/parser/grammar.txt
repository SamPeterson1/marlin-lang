program: item*
item: main | struct | impl | function

main: "main" block-expression

struct: "struct" identifier "{" struct-body "}"
struct-body: struct-member* struct-constructor*
struct-member: type identifier ";"
struct-constructor: "$" parameters block-expression

impl: "impl" type "{" function* "}"
function: "fn" identifier parameters "->" type block-expression
parameters: "(" [type identifier ("," type identifier)*] ")"

statement: loop | if | block | for | while | exit | delete | declaration | assignment ";"
expression: condition
block: "{" statement* "}"
declaration: "let" type identifier "=" expression ";"
assignment: expression "=" expression | expression
for: "for" "(" declaration expression ";" assignment ")" block
while: "while" expression block
exit: ("return" | "break" | "result") [expression] ";"
delete: "delete" expression ";"
loop: "loop" block
if: "if" expression block elif* [else]
elif: "else" "if" expression block

else: "else" block
constructor: ["new"] "$" identifier arguments
new-array: "new" type ("[" expression "]")+
condition: boolean-factor ("or" boolean-factor)*
boolean-factor: comparison ("and" comparison)*
comparison: arithmetic-term (comparator arithmetic-term)*
comparator: "<" | "<=" | "==" | "!=" | ">" | ">="
arithmetic-term: arithmetic-factor (("-" | "+") arithmetic-factor)*
arithmetic-factor: unary (("*" | "/") unary)*
unary: ("!" | "-" | "*" | "&") unary | member-access
member-access: primary (("->" | ".") identifier | "[" expression "]" | arguments)*
arguments: "(" [expression ("," expression)*] ")"
primary: new-array | constructor | loop | if | block | for | while | "(" expression ")" | literal | identifier


type: unit-type ("[]" ["&"])*
unit-type: (primitive-type | identifier) "*"* ["&"]
primitive-type: "int" | "double" | "bool" | "char";
