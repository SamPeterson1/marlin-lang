program: item*
item: import | scope | main

main: "main" block-expression

import: "using" identifier ["." (import | import-group)] ";"
import-group: "{" import ("," import)* "}"

scope: "scope" identifier "{" scope-item* "}"
scope-item: contract | struct | func
contract: "contract" identifier "{" (function-prototype ";") "}"

struct: "struct" identifier ["requires" identifier*] "{" struct-body "}"
struct-body: (struct-member | struct-constructor)*
struct-member: ["public" | "private"] type identifier ["=" literal]
struct-constructor: identifier parameters block-expression

func: function-prototype "@" identifier identifier block-expression
function-prototype: "fn" identifier parameters "->" type
parameters: "(" [type identifier (type identifier ",")*] ")"

statement: loop | if | block | for | while | leave | declaration | putc | assignment
expression: condition
block: "{" statement* "}"
declaration: "let" type identifier "=" expression ";"
assignment: identifier "=" expression
constructor: ["new"] "$" identifier arguments
for: "for" "(" declaration expression ";" assignment ")" block
while: "while" expression block
leave: "leave" [expression] ";"
loop: "loop" block
if: "if" expression block elif* [else]
elif: "else" "if" expression block
else: "else" block
alloc: "new" type ("[" expression "]")+
condition: boolean-factor ("or" boolean-factor)*
boolean-factor: comparison ("and" comparison)*
comparison: arithmetic-term (comparator arithmetic-term)*
comparator: "<" | "<=" | "==" | "!=" | ">" | ">="
arithmetic-term: arithmetic-factor (("-" | "+") arithmetic-factor)*
arithmetic-factor: unary (("*" | "/") unary)*
unary: ("!" | "-")* unary | primary
arguments: "(" [expression (expression ",")*] ")"
primary: call | getc | alloc | loop | if | block | for | while | "(" expression ")" | identifier | literal
call: identifier [arguments] "@" expression
type: ["&"] (primitive-type | identifier) | (primitive-type | identifier) ("[]")*
primitive-type: "int" | "double" | "bool" | "Self";
literal: string | character-literal | number | bool

string: """ (character | escape-sequence)* """
character-literal: "'" character "'"
escape-sequence: "\a" | "\b" | "\f" | "\n" | "\r" | "\t" | "\v" | "\'" | "\"" | "\\"
character: " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "/" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~" | {"A"..."Z"} | {"a"..."z"} | {"0"..."9"} | escape-sequence

number: decimal | integer
decimal: ["-"] digit* ["." digit*] ["d"] | positive-integer "d"
bool: "true" | "false"
integer: ["-"] positive-integer ["i"]
nonnegative-integer: digit+
positive-integer: nonzero-digit digit*
identifier: alphabetic alpha-numeric*

alpha-numeric: alphabetic | digit
alphabetic: {"A"..."Z"} | {"a"..."z"} | "_"
digit = "0" | nonzero-digit
nonzero-digit: "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
